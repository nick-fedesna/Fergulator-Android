buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.12.2'
    }
}
apply plugin: 'com.android.application'

repositories {
    mavenCentral()
    flatDir { dirs 'libs' }
}


android {
    compileSdkVersion 20
    buildToolsVersion "20.0.0"

    defaultConfig {
        applicationId 'com.ferg.afergulator'
        minSdkVersion 15
        targetSdkVersion 20
        versionCode 1
        versionName '1.0'
    }

    productFlavors {
        mobile {}
        glass {}
        fire {
            minSdkVersion 16
        }
        tv {
            minSdkVersion 'L'
            targetSdkVersion 'L'
        }
    }

}

dependencies {
    compile 'com.android.support:support-v4:20.0.0'
    compile 'com.google.guava:guava:15.0'

    compile 'com.jakewharton:butterknife:5.1.2'
    compile 'com.jakewharton.timber:timber:2.4.1'

    glassCompile(name:'gdk', ext:'jar')
    fireCompile(name:'amazon_ui', ext:'aar')

    compile fileTree(dir: 'libs', include: '*.jar')

}

task buildGo(type: Exec, description: 'build Fergulator shared library') {
    workingDir '..'
    commandLine './buildGo'
//    ignoreExitValue true
}

project.afterEvaluate {
    project.android['applicationVariants'].all { variant ->
        Task pkg = tasks.findByPath(':android:package' + variant.name.capitalize())
        pkg.dependsOn(buildGo)
    }
}